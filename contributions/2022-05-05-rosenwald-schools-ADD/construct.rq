PREFIX foaf:    <http://xmlns.com/foaf/0.1/>
PREFIX geojson: <https://purl.org/geojson/vocab#>
PREFIX ncgaz:   <http://n2t.net/ark:/39333/ncg/>
PREFIX ncp:     <http://n2t.net/ark:/39333/ncg/place/>
PREFIX nct:     <http://n2t.net/ark:/39333/ncg/type#>
PREFIX ncv:     <http://n2t.net/ark:/39333/ncg/vocab#>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos:    <http://www.w3.org/2004/02/skos/core#>
PREFIX wd:      <http://www.wikidata.org/entity/>
PREFIX xsd:     <http://www.w3.org/2001/XMLSchema#>

CONSTRUCT {
  ?PLACE
  a nct:School ;
  geojson:geometry ?GEOMETRY ;
  ncv:budgetYear ?budget_year ;
  ncv:county ?COUNTY ;
  ncv:location ?location ;
  ncv:rosenwaldType ?rosenwald_type ;
  ncv:township ?township ;
  ncv:nchpoID ?id_code ;
  ncv:needsReview ?NEEDS_REVIEW ;
  rdfs:seeAlso ?WIKIDATA ;
  skos:note ?note ;
  skos:altLabel ?alt_name ;
  skos:prefLabel ?preflabel ;
  foaf:depiction ?NCHPO_PHOTO ;
  foaf:isPrimaryTopicOf ?NCHPO_PDF ;
  foaf:isPrimaryTopicOf ?FISK ;
  .

  ?DATASET
  a rdf:Bag ;
  rdfs:member ?PLACE ;
  .
}
WHERE {
  BIND (tarql:expandPrefixedName('ncgaz:dataset') AS ?DATASET)
  BIND (tarql:expandPrefixedName(CONCAT('ncp:', ?id)) AS ?PLACE)
  BIND (tarql:expandPrefixedName(CONCAT('ncp:', ?county)) AS ?COUNTY)
  BIND (tarql:expandPrefixedName(CONCAT('wd:', ?wikidata)) AS ?WIKIDATA)
  BIND (URI(?id2_url) AS ?FISK)
  BIND (URI(?id_photo) AS ?NCHPO_PHOTO)
  BIND (URI(REPLACE(?id_url, ' ', '%20')) AS ?NCHPO_PDF)
  BIND (STRDT(LCASE(?needs_review), xsd:boolean) AS ?NEEDS_REVIEW)
  BIND (STRDT(CONCAT('{"type":"Point","coordinates":[',
                     ?longitude, ',', ?latitude, ']}'),
              rdf:JSON)
        AS ?GEOMETRY)
}
